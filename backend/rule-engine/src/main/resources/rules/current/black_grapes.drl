package rules.current;

import com.agriscope.rule_engine.domain.model.CurrentWeatherData
import com.agriscope.rule_engine.domain.model.Seed
import com.agriscope.rule_engine.domain.model.Recommendation
import com.agriscope.rule_engine.domain.enums.SeedType
import com.agriscope.rule_engine.domain.enums.RecommendationType

global java.util.List recommendations;

rule "Black Grapes - Frost Alert"
when
    $seed: Seed(seedType == SeedType.BLACK_GRAPES)
    $weather: CurrentWeatherData(temperature_2m < $seed.frostRiskTemperature)
then
    Recommendation rec = new Recommendation();
    rec.setUserId($weather.getUserId());
    rec.setRecommendedSeed(SeedType.BLACK_GRAPES);
    rec.setRecommendationType(RecommendationType.FROST_ALERT);
    rec.setAdvice("IGNITE_FROST_CANDLES_OR_IRRIGATE");
    rec.setReasoning("Temperature (" + $weather.getTemperature_2m() + "C) below frost risk threshold (" + $seed.getFrostRiskTemperature() + "°C)");
    rec.setWeatherTimestamp($weather.getTime());
    recommendations.add(rec);
end

rule "Black Grapes - Heat Stress Alert"
when
    $seed: Seed(seedType == SeedType.BLACK_GRAPES)
    $weather: CurrentWeatherData(temperature_2m > $seed.heatStressTemperature)
then
    Recommendation rec = new Recommendation();
    rec.setUserId($weather.getUserId());
    rec.setRecommendedSeed(SeedType.BLACK_GRAPES);
    rec.setRecommendationType(RecommendationType.HEAT_ALERT);
    rec.setAdvice("MANAGE_CANOPY_SHADE");
    rec.setReasoning("Temperature (" + $weather.getTemperature_2m() + "C) above heat stress threshold (" + $seed.getHeatStressTemperature() + "°C)");
    rec.setWeatherTimestamp($weather.getTime());
    recommendations.add(rec);
end

rule "Black Grapes - Optimal Conditions"
when
    $seed: Seed(seedType == SeedType.BLACK_GRAPES)
    $weather: CurrentWeatherData(
        temperature_2m >= $seed.minTemperature && temperature_2m <= $seed.maxTemperature,
        rain < $seed.heavyRainThreshold
    )
then
    Recommendation rec = new Recommendation();
    rec.setUserId($weather.getUserId());
    rec.setRecommendedSeed(SeedType.BLACK_GRAPES);
    rec.setRecommendationType(RecommendationType.CONTINUE_NORMAL);
    rec.setAdvice("CONTINUE_NORMAL_BLACK_GRAPES_OPERATIONS");
    rec.setReasoning("Optimal conditions for black grapes - temp: " + $weather.getTemperature_2m() + "C, rain: " + $weather.getRain() + "mm");
    rec.setWeatherTimestamp($weather.getTime());
    recommendations.add(rec);
end