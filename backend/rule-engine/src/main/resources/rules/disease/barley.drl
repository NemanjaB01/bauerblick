package rules.disease;
import com.agriscope.rule_engine.domain.model.DailyWeatherData;
import com.agriscope.rule_engine.domain.model.Seed;
import com.agriscope.rule_engine.domain.enums.SeedType;
import com.agriscope.rule_engine.domain.model.Recommendation
import com.agriscope.rule_engine.domain.enums.RecommendationType;

global java.util.List recommendations;

rule "Barley - Lodging Risk (Rain + Wind)"
    when
        $seed : Seed(seedType == SeedType.BARLEY)
        $day : DailyWeatherData(
            rain_sum > 5.0,
            wind_speed_10m_max > $seed.maxWindTolerance * 0.8
        )
    then
        Recommendation rec = new Recommendation();
        rec.setRecommendedSeed(SeedType.BARLEY);
        rec.setRecommendationType(RecommendationType.PLANNING_ALERT);
        rec.setAdvice("7-Day Forecast: Critical Lodging Risk on " + $day.getDate().toLocalDate());

        rec.setReasoning("Forecast for " + $day.getDate().toLocalDate() + " predicts rain (" + $day.getRain_sum() +
                         "mm) combined with wind (" + $day.getWind_speed_10m_max() + " km/h). This combination often flattens barley.");

        rec.getMetrics().put("forecast_date", $day.getDate().toString());
        rec.getMetrics().put("rain_predicted", $day.getRain_sum());
        rec.getMetrics().put("wind_predicted", $day.getWind_speed_10m_max());

        recommendations.add(rec);
end

rule "Barley - Disease Risk (Cold & Wet)"
    when
        $seed : Seed(seedType == SeedType.BARLEY)
        $day : DailyWeatherData(
            rain_sum > 2.0,
            temperature_2m_max < 22.0
        )
    then
        Recommendation rec = new Recommendation();
        rec.setRecommendedSeed(SeedType.BARLEY);
        rec.setRecommendationType(RecommendationType.DISEASE_PREVENTION);
        rec.setAdvice("Disease Risk: Net Blotch");
        rec.setReasoning("Cool and wet conditions favor Net Blotch. Monitor leaves.");
        rec.getMetrics().put("forecast_date", $day.getDate().toString());
        rec.getMetrics().put("rain_predicted", $day.getRain_sum());
        rec.getMetrics().put("temp_max_predicted", $day.getTemperature_2m_max());
        recommendations.add(rec);
end

rule "Corn - Pest Risk (Corn Borer)"
    when
        $seed : Seed(seedType == SeedType.CORN)
        $day : DailyWeatherData(
            temperature_2m_min > 12.0,
            temperature_2m_max > 20.0,
            temperature_2m_max < 32.0,
            wind_speed_10m_max < 15.0,
            rain_sum < 5.0
        )
    then
        Recommendation rec = new Recommendation();
        rec.setRecommendedSeed(SeedType.CORN);
        rec.setRecommendationType(RecommendationType.PEST_RISK);

        rec.setAdvice("7-Day Forecast: Pest Risk (Corn Borer) on " + $day.getDate().toLocalDate());

        rec.setReasoning("Forecast predicts warm nights (>12Â°C), mild days, and low wind on " +
                         $day.getDate().toLocalDate() + ". " +
                         "Ideal conditions for Corn Borer moths to fly and lay eggs. Scout fields for egg masses.");

        rec.getMetrics().put("forecast_date", $day.getDate().toString());
        rec.getMetrics().put("temp_min_predicted", $day.getTemperature_2m_min());
        rec.getMetrics().put("wind_speed", $day.getWind_speed_10m_max());

        recommendations.add(rec);
end