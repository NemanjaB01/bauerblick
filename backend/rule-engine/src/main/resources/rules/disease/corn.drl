package rules.disease;
import com.agriscope.rule_engine.domain.model.DailyWeatherData;
import com.agriscope.rule_engine.domain.model.Seed;
import com.agriscope.rule_engine.domain.enums.SeedType;
import com.agriscope.rule_engine.domain.model.Recommendation
import com.agriscope.rule_engine.domain.enums.RecommendationType;

global java.util.List recommendations;

rule "Corn - Fungal Disease Risk"
    when
        $seed : Seed(seedType == SeedType.CORN)
        $day : DailyWeatherData(
            rain_sum > $seed.diseaseRainThreshold,
            temperature_2m_min > $seed.diseaseRiskMinTemp
        )
    then
        Recommendation rec = new Recommendation();
        rec.setRecommendedSeed(SeedType.CORN);
        rec.setRecommendationType(RecommendationType.DISEASE_PREVENTION);
        rec.setAdvice("Monitor for Leaf Blight");
        rec.setReasoning("Warm humid forecast favors fungal spread on leaves. Check lower leaves for lesions.");
        rec.getMetrics().put("forecast_date", $day.getDate().toString());
        rec.getMetrics().put("rain", $day.getRain_sum());
        recommendations.add(rec);
end

rule "Corn - Wind Damage Risk"
    when
        $seed : Seed(seedType == SeedType.CORN)
        $day : DailyWeatherData(
            wind_speed_10m_max > $seed.maxWindTolerance
        )
    then
        Recommendation rec = new Recommendation();
        rec.setRecommendedSeed(SeedType.CORN);
        rec.setRecommendationType(RecommendationType.PLANNING_ALERT);
        rec.setAdvice("7-Day Forecast: High Wind Alert on " + $day.getDate().toLocalDate());

        rec.setReasoning("On " + $day.getDate().toLocalDate() + ", wind gusts up to " + $day.getWind_speed_10m_max() +
                         " km/h are predicted. Risk of lodging (stalk breakage). Avoid spraying or machinery work on this day.");

        rec.getMetrics().put("forecast_date", $day.getDate().toString());
        rec.getMetrics().put("wind_speed_predicted", $day.getWind_speed_10m_max());

        recommendations.add(rec);
end