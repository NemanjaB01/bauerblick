package rules.daily.wheat;
import com.agriscope.rule_engine.domain.model.DailyWeatherData;
import com.agriscope.rule_engine.domain.model.Seed;
import com.agriscope.rule_engine.domain.enums.SeedType;
import com.agriscope.rule_engine.domain.model.Recommendation;
import java.time.format.DateTimeFormatter
import com.agriscope.rule_engine.domain.enums.RecommendationType;

global java.util.List recommendations;

rule "Wheat - Disease Risk (Fusarium)"
    when
        $seed : Seed(seedType == SeedType.WHEAT)
        $day : DailyWeatherData(
            rain_sum > $seed.diseaseRainThreshold,
            temperature_2m_max >= $seed.diseaseRiskMinTemp,
            temperature_2m_max <= $seed.diseaseRiskMaxTemp
        )
    then
        Recommendation rec = new Recommendation();
        rec.setRecommendedSeed(SeedType.WHEAT);
        rec.setRecommendationType(RecommendationType.DISEASE_PREVENTION);
        rec.setAdvice("7-Day Forecast Alert: Fusarium Risk detected on " + $day.getDate().toLocalDate());

        rec.setReasoning("In the upcoming 7 days, specifically on " + $day.getDate().toLocalDate() +
                         ", forecast predicts rain (" + $day.getRain_sum() + "mm) with warm temps. " +
                         "These are ideal conditions for Fusarium. Plan fungicide application before this date.");

        rec.getMetrics().put("forecast_date", $day.getDate().toString());
        rec.getMetrics().put("rain_predicted", $day.getRain_sum());

        recommendations.add(rec);
end

rule "Wheat - Nutrient Loss Warning"
    when
        $seed : Seed(seedType == SeedType.WHEAT)
        $day : DailyWeatherData(
            rain_sum > $seed.heavyRainThreshold
        )
    then
        Recommendation rec = new Recommendation();
        rec.setRecommendedSeed(SeedType.WHEAT);
        rec.setRecommendationType(RecommendationType.NUTRIENT_CHECK);
        rec.setAdvice("7-Day Forecast: Nutrient Leaching Risk on " + $day.getDate().toLocalDate());

        rec.setReasoning("Heavy rain (" + $day.getRain_sum() + "mm) is predicted on " + $day.getDate().toLocalDate() +
                         ". This may wash away Nitrogen. Consider checking soil status after this date.");

        rec.getMetrics().put("forecast_date", $day.getDate().toString());
        rec.getMetrics().put("rain_predicted", $day.getRain_sum());

        recommendations.add(rec);
end