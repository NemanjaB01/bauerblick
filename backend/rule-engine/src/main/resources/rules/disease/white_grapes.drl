package rules.disease;
import com.agriscope.rule_engine.domain.model.DailyWeatherData;
import com.agriscope.rule_engine.domain.model.Seed;
import com.agriscope.rule_engine.domain.enums.SeedType;
import com.agriscope.rule_engine.domain.model.Recommendation;
import com.agriscope.rule_engine.domain.enums.RecommendationType;
import com.agriscope.rule_engine.domain.model.FieldStatus
import com.agriscope.rule_engine.domain.enums.GrowthStage

global java.util.List recommendations;

rule "White Grapes - Downy Mildew Alert Dynamic"
    when
        $seed : Seed(seedType == SeedType.WHITE_GRAPES)
        $field : FieldStatus(seedType == SeedType.WHITE_GRAPES, stage != GrowthStage.READY)
        $tempLimit : Number() from $seed.diseaseRiskMinTemp + $seed.getParam("disease_temp_offset", 0.0)
        $rainLimit : Number() from $seed.diseaseRainThreshold * $seed.getParam("disease_rain_factor", 1.0)

        $day : DailyWeatherData(
            temperature_2m_min > $tempLimit.doubleValue(),
            rain_sum > $rainLimit.doubleValue()
        )
    then
        Recommendation rec = new Recommendation();
        rec.setRecommendedSeed(SeedType.WHITE_GRAPES);
        rec.setRecommendationType(RecommendationType.DISEASE_PREVENTION);

        rec.setAdvice("7-Day Forecast: Downy Mildew Risk on " + $day.getDate().toLocalDate());
        rec.setReasoning("Wet conditions (> " + String.format("%.1f", $rainLimit) + "mm) & Temp > " +
                         String.format("%.1f", $tempLimit) + "C favor Downy Mildew.");

        rec.getMetrics().put("forecast_date", $day.getDate().toString());
        rec.getMetrics().put("rain_predicted", $day.getRain_sum());
        recommendations.add(rec);
end

rule "White Grapes - Pest Risk (Berry Moth) Dynamic"
    when
        $seed : Seed(seedType == SeedType.WHITE_GRAPES)
        $field : FieldStatus(seedType == SeedType.WHITE_GRAPES, stage != GrowthStage.READY)
        $pestTempLimit : Number() from 14.0 + $seed.getParam("pest_temp_offset", 0.0)

        $day : DailyWeatherData(
            temperature_2m_min > $pestTempLimit.doubleValue(),
            rain_sum < 2.0
        )
    then
        Recommendation rec = new Recommendation();
        rec.setRecommendedSeed(SeedType.WHITE_GRAPES);
        rec.setRecommendationType(RecommendationType.PEST_RISK);

        rec.setAdvice("7-Day Forecast: Berry Moth Risk on " + $day.getDate().toLocalDate());
        rec.setReasoning("Warm nights (> " + String.format("%.1f", $pestTempLimit) +
                         "C) and dry weather favor moth activity.");

        rec.getMetrics().put("forecast_date", $day.getDate().toString());
        rec.getMetrics().put("temp_min_predicted", $day.getTemperature_2m_min());
        recommendations.add(rec);
end