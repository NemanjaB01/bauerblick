package rules.irrigation;

import com.agriscope.rule_engine.domain.model.Seed;
import com.agriscope.rule_engine.domain.model.FarmDetails;
import com.agriscope.rule_engine.domain.model.Recommendation;
import com.agriscope.rule_engine.domain.dto.DailyAnalysis;
import com.agriscope.rule_engine.domain.enums.SeedType;
import com.agriscope.rule_engine.domain.enums.SoilType;
import com.agriscope.rule_engine.domain.enums.RecommendationType;

global java.util.List recommendations;

rule "Delay Irrigation - BARLEY"
    salience 99
    activation-group "barley-irrigation"
    when
        $farm : FarmDetails($soil : soilType)
        $seed : Seed(seedType == SeedType.BARLEY)
        $calc : WaterDeficitResult(
            seed == $seed,
            deficit > $seed.allowedWaterDeficit
        )
        $analysis : DailyAnalysis(
            currentSoilMoisture < $seed.minSoilMoisture,
            totalRain > 5.0
        )
    then
        Recommendation rec = new Recommendation();
        rec.setRecommendedSeed(SeedType.BARLEY);
        rec.setRecommendationType(RecommendationType.DELAY_IRRIGATION);
        rec.setAdvice("Delay Irrigation for Barley - Rain Expected");
        rec.setReasoning("Rain forecast (" + String.format("%.1f", $analysis.getTotalRain()) +
            " mm) may reduce deficit. Monitor after rain.");
        rec.getMetrics().put("deficit_amount", $calc.getDeficit());
        rec.getMetrics().put("forecast_rain", $analysis.getTotalRain());
        recommendations.add(rec);
end

rule "Irrigation - BARLEY"
    salience 50
    activation-group "barley-irrigation"
    when
        $farm : FarmDetails($soil : soilType)
        $seed : Seed(seedType == SeedType.BARLEY)
        $calc : WaterDeficitResult(seed == $seed, deficit > $seed.allowedWaterDeficit)
        $analysis : DailyAnalysis(currentSoilMoisture < $seed.minSoilMoisture)
        not DailyAnalysis(totalRain > 5.0)
    then
        Recommendation rec = new Recommendation();
        rec.setRecommendedSeed(SeedType.BARLEY);
        rec.setRecommendationType(RecommendationType.IRRIGATE_NOW);
        rec.setAdvice("Irrigate Barley: " + String.format("%.1f", $calc.getDeficit()) + " L/m2 needed");
        rec.setReasoning("Barley moisture critical on " + $soil + " soil.");
        rec.getMetrics().put("deficit_amount", $calc.getDeficit());
        rec.getMetrics().put("soil_moisture", $analysis.getCurrentSoilMoisture());
        recommendations.add(rec);
end

rule "Irrigation Soon Warning - BARLEY"
    salience 40
    activation-group "barley-irrigation"
    when
        $farm : FarmDetails($soil : soilType)
        $seed : Seed(seedType == SeedType.BARLEY)
        $calc : WaterDeficitResult(
            seed == $seed,
            deficit > $seed.allowedWaterDeficit * 0.7,
            deficit <= $seed.allowedWaterDeficit
        )
        $analysis : DailyAnalysis(
            currentSoilMoisture >= $seed.minSoilMoisture * 0.8,
            currentSoilMoisture < $seed.minSoilMoisture
        )
        not DailyAnalysis(totalRain > 5.0)
    then
        Recommendation rec = new Recommendation();
        rec.setRecommendedSeed(SeedType.BARLEY);
        rec.setRecommendationType(RecommendationType.IRRIGATE_SOON);
        rec.setAdvice("Plan Irrigation for Barley Soon");
        rec.setReasoning("Soil moisture is declining (" +
            String.format("%.1f%%", $analysis.getCurrentSoilMoisture() * 100) +
            "). Deficit: " + String.format("%.1f", $calc.getDeficit()) + " mm on " + $soil + " soil.");
        rec.getMetrics().put("deficit_amount", $calc.getDeficit());
        rec.getMetrics().put("soil_moisture", $analysis.getCurrentSoilMoisture());
        recommendations.add(rec);
end

rule "No Action Needed - BARLEY"
    salience 10
    activation-group "barley-irrigation"
    when
        $farm : FarmDetails($soil : soilType)
        $seed : Seed(seedType == SeedType.BARLEY)
        $calc : WaterDeficitResult(
            seed == $seed,
            deficit <= $seed.allowedWaterDeficit * 0.6
        )
        $analysis : DailyAnalysis(
            currentSoilMoisture >= $seed.minSoilMoisture
        )
    then
        Recommendation rec = new Recommendation();
        rec.setRecommendedSeed(SeedType.BARLEY);
        rec.setRecommendationType(RecommendationType.CONTINUE_NORMAL);
        rec.setAdvice("Barley Conditions Normal");
        rec.setReasoning("Barley has adequate moisture (" +
            String.format("%.1f%%", $analysis.getCurrentSoilMoisture() * 100) +
            ") and manageable deficit (" + String.format("%.1f", $calc.getDeficit()) + " mm).");
        rec.getMetrics().put("deficit_amount", $calc.getDeficit());
        rec.getMetrics().put("soil_moisture", $analysis.getCurrentSoilMoisture());
        recommendations.add(rec);
end

rule "Monitor Conditions - BARLEY (Fallback)"
    salience 1
    activation-group "barley-irrigation"
    when
        $farm : FarmDetails($soil : soilType)
        $seed : Seed(seedType == SeedType.BARLEY, $displayName : displayName)
        $calc : WaterDeficitResult(seed == $seed)
        $analysis : DailyAnalysis()
    then
        Recommendation rec = new Recommendation();
        rec.setRecommendedSeed(SeedType.BARLEY);
        rec.setRecommendationType(RecommendationType.MONITOR_CONDITIONS);
        rec.setAdvice("Monitor Barley Conditions");
        rec.setReasoning("Barley conditions stable. " +
            "Deficit: " + String.format("%.1f", $calc.getDeficit()) + " mm. " +
            "Soil moisture: " + String.format("%.1f%%", $analysis.getCurrentSoilMoisture() * 100) +
            " on " + $soil + " soil. Continue regular monitoring.");
        rec.getMetrics().put("deficit_amount", $calc.getDeficit());
        rec.getMetrics().put("soil_moisture", $analysis.getCurrentSoilMoisture());
        rec.getMetrics().put("rule_fired", "barley-monitor-fallback");
        recommendations.add(rec);
end