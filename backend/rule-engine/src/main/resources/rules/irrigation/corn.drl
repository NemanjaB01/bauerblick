package rules.irrigation;

import com.agriscope.rule_engine.domain.model.Seed;
import com.agriscope.rule_engine.domain.model.FarmDetails;
import com.agriscope.rule_engine.domain.model.Recommendation;
import com.agriscope.rule_engine.domain.dto.DailyAnalysis;
import com.agriscope.rule_engine.domain.enums.SeedType;
import com.agriscope.rule_engine.domain.enums.SoilType;
import com.agriscope.rule_engine.domain.enums.RecommendationType;

global java.util.List recommendations;

rule "Delay Irrigation - CORN "
    salience 99
    activation-group "corn-irrigation"
    when
        $farm : FarmDetails($soil : soilType)
        $seed : Seed(seedType == SeedType.CORN)
        $calc : WaterDeficitResult(
            seed == $seed,
            deficit > $seed.allowedWaterDeficit * 0.8
        )
        $analysis : DailyAnalysis(
            currentSoilMoisture < $seed.minSoilMoisture,
            totalRain > 10.0
        )
    then
        Recommendation rec = new Recommendation();
        rec.setRecommendedSeed(SeedType.CORN);
        rec.setRecommendationType(RecommendationType.DELAY_IRRIGATION);
        rec.setAdvice("Delay Irrigation for Corn - Heavy Rain Expected");
        rec.setReasoning("Heavy rain forecast (" +
            String.format("%.1f", $analysis.getTotalRain()) +
            " mm). Corn is sensitive to waterlogging. Ensure good drainage.");
        rec.getMetrics().put("deficit_amount", $calc.getDeficit());
        rec.getMetrics().put("forecast_rain", $analysis.getTotalRain());
        rec.getMetrics().put("soil_moisture", $analysis.getCurrentSoilMoisture());
        recommendations.add(rec);
end

rule "Irrigation - CORN Dynamic"
    salience 50
    activation-group "corn-irrigation"
    when
        $farm : FarmDetails($soil : soilType)
        $seed : Seed(seedType == SeedType.CORN)
        $calc : WaterDeficitResult(
            seed == $seed,
            deficit > $seed.allowedWaterDeficit * 0.9
        )
        $analysis : DailyAnalysis(
            currentSoilMoisture < $seed.minSoilMoisture * 1.1
        )
        not DailyAnalysis(totalRain > 10.0)
    then
        Recommendation rec = new Recommendation();
        rec.setRecommendedSeed(SeedType.CORN);
        rec.setRecommendationType(RecommendationType.IRRIGATE_NOW);
        rec.setAdvice("URGENT: Irrigate Corn Immediately");
        rec.setReasoning("Corn is highly sensitive to water stress, especially during tasseling. " +
            "Deficit: " + String.format("%.1f", $calc.getDeficit()) +
            " mm on " + $soil + " soil. Yield loss likely if not irrigated.");
        rec.getMetrics().put("deficit_amount", $calc.getDeficit());
        rec.getMetrics().put("soil_moisture", $analysis.getCurrentSoilMoisture());
        rec.getMetrics().put("crop_stage", "water_critical");
        recommendations.add(rec);
end



rule "Irrigation Soon Warning - CORN"
    salience 40
    activation-group "corn-irrigation"
    when
        $farm : FarmDetails($soil : soilType)
        $seed : Seed(seedType == SeedType.CORN)
        $calc : WaterDeficitResult(
            seed == $seed,
            deficit > $seed.allowedWaterDeficit * 0.6,
            deficit <= $seed.allowedWaterDeficit * 0.9
        )
        $analysis : DailyAnalysis(
            currentSoilMoisture >= $seed.minSoilMoisture * 0.9,
            currentSoilMoisture < $seed.minSoilMoisture * 1.1
        )
        not DailyAnalysis(totalRain > 10.0)
    then
        Recommendation rec = new Recommendation();
        rec.setRecommendedSeed(SeedType.CORN);
        rec.setRecommendationType(RecommendationType.IRRIGATE_SOON);
        rec.setAdvice("Prepare Irrigation for Corn");
        rec.setReasoning("Corn water demand increasing. Critical growth stage approaching. " +
            "Moisture: " + String.format("%.1f%%", $analysis.getCurrentSoilMoisture() * 100) +
            ". Deficit: " + String.format("%.1f", $calc.getDeficit()) + " mm. " +
            "Corn requires consistent moisture for optimal yield.");
        rec.getMetrics().put("deficit_amount", $calc.getDeficit());
        rec.getMetrics().put("soil_moisture", $analysis.getCurrentSoilMoisture());
        recommendations.add(rec);
end

rule "No Action Needed - CORN"
    salience 10
    activation-group "corn-irrigation"
    when
        $farm : FarmDetails($soil : soilType)
        $seed : Seed(seedType == SeedType.CORN)
        $calc : WaterDeficitResult(
            seed == $seed,
            deficit <= $seed.allowedWaterDeficit * 0.6
        )
        $analysis : DailyAnalysis(
            currentSoilMoisture >= $seed.minSoilMoisture
        )
    then
        Recommendation rec = new Recommendation();
        rec.setRecommendedSeed(SeedType.CORN);
        rec.setRecommendationType(RecommendationType.CONTINUE_NORMAL);
        rec.setAdvice("Corn Conditions Optimal");
        rec.setReasoning("Corn has excellent moisture (" +
            String.format("%.1f%%", $analysis.getCurrentSoilMoisture() * 100) +
            ") for growth. Deficit: " + String.format("%.1f", $calc.getDeficit()) + " mm.");
        rec.getMetrics().put("deficit_amount", $calc.getDeficit());
        rec.getMetrics().put("soil_moisture", $analysis.getCurrentSoilMoisture());
        recommendations.add(rec);
end


rule "Monitor Conditions - CORN (Fallback)"
    salience 1
    activation-group "corn-irrigation"
    when
        $farm : FarmDetails($soil : soilType)
        $seed : Seed(seedType == SeedType.CORN, $displayName : displayName)
        $calc : WaterDeficitResult(seed == $seed)
        $analysis : DailyAnalysis()
    then
        Recommendation rec = new Recommendation();
        rec.setRecommendedSeed(SeedType.CORN);
        rec.setRecommendationType(RecommendationType.MONITOR_CONDITIONS);
        rec.setAdvice("Monitor Corn Conditions");
        rec.setReasoning("Corn requires careful water management. " +
            "Current deficit: " + String.format("%.1f", $calc.getDeficit()) + " mm. " +
            "Soil moisture: " + String.format("%.1f%%", $analysis.getCurrentSoilMoisture() * 100) +
            " on " + $soil + " soil. Corn is sensitive to water stress.");
        rec.getMetrics().put("deficit_amount", $calc.getDeficit());
        rec.getMetrics().put("soil_moisture", $analysis.getCurrentSoilMoisture());
        rec.getMetrics().put("rule_fired", "corn-monitor-fallback");
        recommendations.add(rec);
end