package rules.irrigation;

import com.agriscope.rule_engine.domain.model.Seed;
import com.agriscope.rule_engine.domain.model.FarmDetails;
import com.agriscope.rule_engine.domain.model.Recommendation;
import com.agriscope.rule_engine.domain.dto.DailyAnalysis;
import com.agriscope.rule_engine.domain.enums.SeedType;
import com.agriscope.rule_engine.domain.enums.SoilType;
import com.agriscope.rule_engine.domain.enums.RecommendationType;

global java.util.List recommendations;

declare WaterDeficitResult
    seed : Seed
    deficit : double
    effectiveRain : double
    plantDemand : double
end

function double getSoilEfficiency(SoilType type) {
    if (type == SoilType.CLAY) return 0.95;
    if (type == SoilType.LOAM) return 0.85;
    if (type == SoilType.SILT) return 0.80;
    if (type == SoilType.PEAT) return 0.80;
    if (type == SoilType.CHALKY) return 0.70;
    return 0.80;
}

rule "Calculate Water Deficit Generic"
    salience 100
    when
        $farm     : FarmDetails($soil : soilType)
        $analysis : DailyAnalysis()
        $seed     : Seed($kc : seedCoefficient)
    then
        double demand = $analysis.getTotalEt0() * $kc;
        double effRain = $analysis.getTotalRain() * getSoilEfficiency($soil);
        double result = demand - effRain;

        WaterDeficitResult calc = new WaterDeficitResult();
        calc.setSeed($seed);
        calc.setDeficit(result);
        calc.setEffectiveRain(effRain);
        calc.setPlantDemand(demand);
        insert(calc);
end

rule "Monitor Conditions - All Crops"
    salience 10
    when
        $seed : Seed()
        $calc : WaterDeficitResult(seed == $seed, deficit <= $seed.allowedWaterDeficit)
        $analysis : DailyAnalysis(currentSoilMoisture >= $seed.minSoilMoisture)
    then
        Recommendation rec = new Recommendation();
        rec.setRecommendedSeed($seed.getSeedType());
        rec.setRecommendationType(RecommendationType.MONITOR_CONDITIONS);
        rec.setAdvice("No Irrigation Needed for " + $seed.getDisplayName());

        String action;
        if ($calc.getDeficit() <= 0.0) {
             action = String.format(
                 "Water Surplus. Forecasted rain exceeds crop needs by %.1f mm. Moisture: %.1f%%",
                 Math.abs($calc.getDeficit()),
                 $analysis.getCurrentSoilMoisture() * 100
             );
        } else {
             action = String.format(
                "Deficit (%.1f mm) is minor and within tolerance (%.1f mm). Moisture: %.1f%%",
                $calc.getDeficit(),
                $seed.getAllowedWaterDeficit(),
                $analysis.getCurrentSoilMoisture() * 100
            );
        }

        rec.setReasoning("Conditions optimal. " + action);

        rec.getMetrics().put("deficit_amount", $calc.getDeficit());
        recommendations.add(rec);
end