package rules.irrigation;

import com.agriscope.rule_engine.domain.model.Seed;
import com.agriscope.rule_engine.domain.model.FarmDetails;
import com.agriscope.rule_engine.domain.model.Recommendation;
import com.agriscope.rule_engine.domain.dto.DailyAnalysis;
import com.agriscope.rule_engine.domain.enums.SeedType;
import com.agriscope.rule_engine.domain.enums.SoilType;
import com.agriscope.rule_engine.domain.enums.RecommendationType;

global java.util.List recommendations;

rule "Delay Irrigation - PUMPKIN "
    salience 99
    activation-group "pumpkin-irrigation"
    when
        $farm : FarmDetails($soil : soilType)
        $seed : Seed(seedType == SeedType.PUMPKIN)
        $calc : WaterDeficitResult(
            seed == $seed,
            deficit > $seed.allowedWaterDeficit * 0.6
        )
        $analysis : DailyAnalysis(
            currentSoilMoisture < $seed.minSoilMoisture,
            totalRain > 6.0
        )
    then
        Recommendation rec = new Recommendation();
        rec.setRecommendedSeed(SeedType.PUMPKIN);
        rec.setRecommendationType(RecommendationType.DELAY_IRRIGATION);
        rec.setAdvice("Delay Irrigation for Pumpkin - Heavy Rain Expected");
        rec.setReasoning("Pumpkins are sensitive to overwatering. Heavy rain forecast (" +
            String.format("%.1f", $analysis.getTotalRain()) +
            " mm) may cause fungal issues. Wait for soil to drain.");
        rec.getMetrics().put("deficit_amount", $calc.getDeficit());
        rec.getMetrics().put("forecast_rain", $analysis.getTotalRain());
        rec.getMetrics().put("soil_moisture", $analysis.getCurrentSoilMoisture());
        recommendations.add(rec);
end

rule "Irrigation - PUMPKIN Dynamic"
    salience 50
    activation-group "pumpkin-irrigation"
    when
        $farm : FarmDetails($soil : soilType)
        $seed : Seed(seedType == SeedType.PUMPKIN)
        $calc : WaterDeficitResult(
            seed == $seed,
            deficit > $seed.allowedWaterDeficit * 0.8
        )
        $analysis : DailyAnalysis(
            currentSoilMoisture < $seed.minSoilMoisture * 1.1
        )
        not DailyAnalysis(totalRain > 6.0)
    then
        Recommendation rec = new Recommendation();
        rec.setRecommendedSeed(SeedType.PUMPKIN);
        rec.setRecommendationType(RecommendationType.IRRIGATE_NOW);
        rec.setAdvice("URGENT: Irrigate Pumpkin");
        rec.setReasoning("Pumpkins are highly sensitive to water stress. " +
            "Deficit: " + String.format("%.1f", $calc.getDeficit()) +
            " mm on " + $soil + " soil. Fruits may crack or stop growing.");
        rec.getMetrics().put("deficit_amount", $calc.getDeficit());
        rec.getMetrics().put("soil_moisture", $analysis.getCurrentSoilMoisture());
        recommendations.add(rec);
end


rule "Irrigation Soon Warning - PUMPKIN"
    salience 40
    activation-group "pumpkin-irrigation"
    when
        $farm : FarmDetails($soil : soilType)
        $seed : Seed(seedType == SeedType.PUMPKIN)
        $calc : WaterDeficitResult(
            seed == $seed,
            deficit > $seed.allowedWaterDeficit * 0.5,
            deficit <= $seed.allowedWaterDeficit * 0.8
        )
        $analysis : DailyAnalysis(
            currentSoilMoisture >= $seed.minSoilMoisture * 0.9,
            currentSoilMoisture < $seed.minSoilMoisture * 1.1
        )
        not DailyAnalysis(totalRain > 6.0)
    then
        Recommendation rec = new Recommendation();
        rec.setRecommendedSeed(SeedType.PUMPKIN);
        rec.setRecommendationType(RecommendationType.IRRIGATE_SOON);
        rec.setAdvice("Prepare Irrigation for Pumpkin");
        rec.setReasoning("Pumpkins showing early stress signs. " +
            "Moisture: " + String.format("%.1f%%", $analysis.getCurrentSoilMoisture() * 100) +
            ". Deficit: " + String.format("%.1f", $calc.getDeficit()) + " mm. " +
            "Pumpkins need consistent moisture for fruit development.");
        rec.getMetrics().put("deficit_amount", $calc.getDeficit());
        rec.getMetrics().put("soil_moisture", $analysis.getCurrentSoilMoisture());
        recommendations.add(rec);
end

rule "No Action Needed - PUMPKIN"
    salience 10
    activation-group "pumpkin-irrigation"
    when
        $farm : FarmDetails($soil : soilType)
        $seed : Seed(seedType == SeedType.PUMPKIN)
        $calc : WaterDeficitResult(
            seed == $seed,
            deficit <= $seed.allowedWaterDeficit * 0.5
        )
        $analysis : DailyAnalysis(
            currentSoilMoisture >= $seed.minSoilMoisture
        )
    then
        Recommendation rec = new Recommendation();
        rec.setRecommendedSeed(SeedType.PUMPKIN);
        rec.setRecommendationType(RecommendationType.CONTINUE_NORMAL);
        rec.setAdvice("Pumpkin Conditions Optimal");
        rec.setReasoning("Pumpkins have excellent moisture (" +
            String.format("%.1f%%", $analysis.getCurrentSoilMoisture() * 100) +
            ") and minimal deficit (" + String.format("%.1f", $calc.getDeficit()) + " mm).");
        rec.getMetrics().put("deficit_amount", $calc.getDeficit());
        rec.getMetrics().put("soil_moisture", $analysis.getCurrentSoilMoisture());
        recommendations.add(rec);
end


rule "Monitor Conditions - PUMPKIN (Fallback)"
    salience 1
    activation-group "pumpkin-irrigation"
    when
        $farm : FarmDetails($soil : soilType)
        $seed : Seed(seedType == SeedType.PUMPKIN, $displayName : displayName)
        $calc : WaterDeficitResult(seed == $seed)
        $analysis : DailyAnalysis()
    then
        Recommendation rec = new Recommendation();
        rec.setRecommendedSeed(SeedType.PUMPKIN);
        rec.setRecommendationType(RecommendationType.MONITOR_CONDITIONS);
        rec.setAdvice("Monitor Pumpkin Conditions");
        rec.setReasoning("Pumpkins require consistent moisture but are prone to fungal issues. " +
            "Current deficit: " + String.format("%.1f", $calc.getDeficit()) + " mm. " +
            "Soil moisture: " + String.format("%.1f%%", $analysis.getCurrentSoilMoisture() * 100) +
            " on " + $soil + " soil. Monitor for signs of stress or overwatering.");
        rec.getMetrics().put("deficit_amount", $calc.getDeficit());
        rec.getMetrics().put("soil_moisture", $analysis.getCurrentSoilMoisture());
        rec.getMetrics().put("rule_fired", "pumpkin-monitor-fallback");
        recommendations.add(rec);
end