package rules.irrigation;

import com.agriscope.rule_engine.domain.model.Seed;
import com.agriscope.rule_engine.domain.model.FarmDetails;
import com.agriscope.rule_engine.domain.model.Recommendation;
import com.agriscope.rule_engine.domain.dto.DailyAnalysis;
import com.agriscope.rule_engine.domain.enums.SeedType;
import com.agriscope.rule_engine.domain.enums.SoilType;
import com.agriscope.rule_engine.domain.enums.RecommendationType;

global java.util.List recommendations;

rule "Delay Irrigation - WHEAT"
    salience 99
    activation-group "wheat-irrigation"
    when
        $farm : FarmDetails($soil : soilType)
        $seed : Seed(seedType == SeedType.WHEAT)
        $calc : WaterDeficitResult(
            seed == $seed,
            deficit > $seed.allowedWaterDeficit
        )
        $analysis : DailyAnalysis(
            currentSoilMoisture < $seed.minSoilMoisture,
            totalRain > 5.0
        )
    then
        Recommendation rec = new Recommendation();
        rec.setRecommendedSeed(SeedType.WHEAT);
        rec.setRecommendationType(RecommendationType.DELAY_IRRIGATION);
        rec.setAdvice("Delay Irrigation for Wheat - Rain Expected");
        rec.setReasoning("Rain forecast (" +
            String.format("%.1f", $analysis.getTotalRain()) +
            " mm) should reduce water deficit. Wheat can tolerate short dry periods.");
        rec.getMetrics().put("deficit_amount", $calc.getDeficit());
        rec.getMetrics().put("forecast_rain", $analysis.getTotalRain());
        rec.getMetrics().put("soil_moisture", $analysis.getCurrentSoilMoisture());
        recommendations.add(rec);
end

rule "Irrigation - WHEAT Dynamic"
    salience 50
    activation-group "wheat-irrigation"
    when
        $farm : FarmDetails($soil : soilType)
        $seed : Seed(seedType == SeedType.WHEAT)
        $calc : WaterDeficitResult(
            seed == $seed,
            deficit > $seed.allowedWaterDeficit
        )
        $analysis : DailyAnalysis(
            currentSoilMoisture < $seed.minSoilMoisture
        )
        not DailyAnalysis(totalRain > 5.0)
    then
        Recommendation rec = new Recommendation();
        rec.setRecommendedSeed(SeedType.WHEAT);
        rec.setRecommendationType(RecommendationType.IRRIGATE_NOW);
        rec.setAdvice("Irrigate Wheat: " + String.format("%.1f", $calc.getDeficit()) + " mm needed");
        rec.setReasoning("Wheat moisture critical on " + $soil +
            " soil. Yield may be affected if not irrigated.");
        rec.getMetrics().put("deficit_amount", $calc.getDeficit());
        rec.getMetrics().put("soil_moisture", $analysis.getCurrentSoilMoisture());
        recommendations.add(rec);
end

rule "Irrigation Soon Warning - WHEAT"
    salience 40
    activation-group "wheat-irrigation"
    when
        $farm : FarmDetails($soil : soilType)
        $seed : Seed(seedType == SeedType.WHEAT)
        $calc : WaterDeficitResult(
            seed == $seed,
            deficit > $seed.allowedWaterDeficit * 0.7,
            deficit <= $seed.allowedWaterDeficit
        )
        $analysis : DailyAnalysis(
            currentSoilMoisture >= $seed.minSoilMoisture * 0.8,
            currentSoilMoisture < $seed.minSoilMoisture
        )
        not DailyAnalysis(totalRain > 5.0)
    then
        Recommendation rec = new Recommendation();
        rec.setRecommendedSeed(SeedType.WHEAT);
        rec.setRecommendationType(RecommendationType.IRRIGATE_SOON);
        rec.setAdvice("Plan Irrigation for Wheat Soon");
        rec.setReasoning("Wheat approaching critical moisture levels. " +
            "Moisture: " + String.format("%.1f%%", $analysis.getCurrentSoilMoisture() * 100) +
            ". Deficit: " + String.format("%.1f", $calc.getDeficit()) + " mm.");
        rec.getMetrics().put("deficit_amount", $calc.getDeficit());
        rec.getMetrics().put("soil_moisture", $analysis.getCurrentSoilMoisture());
        recommendations.add(rec);
end

rule "No Action Needed - WHEAT"
    salience 10
    activation-group "wheat-irrigation"
    when
        $farm : FarmDetails($soil : soilType)
        $seed : Seed(seedType == SeedType.WHEAT)
        $calc : WaterDeficitResult(
            seed == $seed,
            deficit <= $seed.allowedWaterDeficit * 0.7
        )
        $analysis : DailyAnalysis(
            currentSoilMoisture >= $seed.minSoilMoisture
        )
    then
        Recommendation rec = new Recommendation();
        rec.setRecommendedSeed(SeedType.WHEAT);
        rec.setRecommendationType(RecommendationType.CONTINUE_NORMAL);
        rec.setAdvice("Wheat Conditions Normal");
        rec.setReasoning("Wheat has adequate moisture (" +
            String.format("%.1f%%", $analysis.getCurrentSoilMoisture() * 100) +
            ") and manageable deficit (" + String.format("%.1f", $calc.getDeficit()) + " mm).");
        rec.getMetrics().put("deficit_amount", $calc.getDeficit());
        rec.getMetrics().put("soil_moisture", $analysis.getCurrentSoilMoisture());
        recommendations.add(rec);
end

rule "Monitor Conditions - WHEAT (Fallback)"
    salience 1
    activation-group "wheat-irrigation"
    when
        $farm : FarmDetails($soil : soilType)
        $seed : Seed(seedType == SeedType.WHEAT, $displayName : displayName)
        $calc : WaterDeficitResult(seed == $seed)
        $analysis : DailyAnalysis()
    then
        Recommendation rec = new Recommendation();
        rec.setRecommendedSeed(SeedType.WHEAT);
        rec.setRecommendationType(RecommendationType.MONITOR_CONDITIONS);
        rec.setAdvice("Monitor Wheat Conditions");
        rec.setReasoning("Wheat conditions within normal monitoring range. " +
            "Deficit: " + String.format("%.1f", $calc.getDeficit()) + " mm. " +
            "Soil moisture: " + String.format("%.1f%%", $analysis.getCurrentSoilMoisture() * 100) +
            " on " + $soil + " soil. Check again in 24 hours.");
        rec.getMetrics().put("deficit_amount", $calc.getDeficit());
        rec.getMetrics().put("soil_moisture", $analysis.getCurrentSoilMoisture());
        rec.getMetrics().put("rule_fired", "wheat-monitor-fallback");
        recommendations.add(rec);
end