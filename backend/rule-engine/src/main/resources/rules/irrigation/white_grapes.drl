package rules.irrigation;

import com.agriscope.rule_engine.domain.model.Seed;
import com.agriscope.rule_engine.domain.model.FarmDetails;
import com.agriscope.rule_engine.domain.model.Recommendation;
import com.agriscope.rule_engine.domain.model.FieldStatus;
import com.agriscope.rule_engine.domain.dto.DailyAnalysis;
import com.agriscope.rule_engine.domain.enums.SeedType;
import com.agriscope.rule_engine.domain.enums.SoilType;
import com.agriscope.rule_engine.domain.enums.RecommendationType;
import com.agriscope.rule_engine.domain.enums.GrowthStage;

global java.util.List recommendations;


rule "White Grapes - Ready - Stop Irrigation"
    salience 200
    activation-group "whitegrapes-irrigation"
    when
        $seed   : Seed(seedType == SeedType.WHITE_GRAPES)
        $status : FieldStatus(seedType == SeedType.WHITE_GRAPES, stage == GrowthStage.READY, $fid : fieldId)
    then
        Recommendation rec = new Recommendation();
        rec.setFieldId($fid);
        rec.setRecommendedSeed(SeedType.WHITE_GRAPES);
        rec.setRecommendationType(RecommendationType.CONTINUE_NORMAL);
        rec.setAdvice("STOP Irrigation - Harvest Approaching");
        rec.setReasoning("White grapes are in 'Ready' stage. Irrigation now can dilute acidity and aromatics, reducing wine quality.");
        recommendations.add(rec);
end

rule "Delay Irrigation - WHITE GRAPES"
    salience 99
    activation-group "whitegrapes-irrigation"
    when
        $status : FieldStatus(seedType == SeedType.WHITE_GRAPES, $stage : stage, $fid : fieldId)
        $farm   : FarmDetails($soil : soilType)
        $seed   : Seed(seedType == SeedType.WHITE_GRAPES)

        $calc   : WaterDeficitResult(
            seed == $seed,
            fieldId == $fid,
            deficit > $seed.allowedWaterDeficit * $seed.getParam("delay_deficit_factor", 1.1)
        )

        $analysis : DailyAnalysis(
            currentSoilMoisture < $seed.minSoilMoisture,
            totalRain > $seed.heavyRainThreshold
        )
    then
        Recommendation rec = new Recommendation();
        rec.setFieldId($fid);
        rec.setRecommendedSeed(SeedType.WHITE_GRAPES);
        rec.setRecommendationType(RecommendationType.DELAY_IRRIGATION);
        rec.setAdvice("Delay Irrigation (" + $stage + ") - Rain Forecast");
        rec.setReasoning("Rain forecast (" + String.format("%.1f", $analysis.getTotalRain()) +
                         "mm) expected. Mild stress maintains acidity balance.");
        rec.getMetrics().put("deficit_amount", $calc.getDeficit());
        recommendations.add(rec);
end

rule "Irrigation - WHITE GRAPES - Urgent"
    salience 50
    activation-group "whitegrapes-irrigation"
    when
        $status : FieldStatus(seedType == SeedType.WHITE_GRAPES, $stage : stage, $fid : fieldId)
        $farm   : FarmDetails($soil : soilType)
        $seed   : Seed(seedType == SeedType.WHITE_GRAPES)

        $calc : WaterDeficitResult(
            seed == $seed,
            fieldId == $fid,
            deficit > $seed.allowedWaterDeficit * $seed.getParam("urgent_deficit_factor", 1.3)
        )

        $analysis : DailyAnalysis(
            currentSoilMoisture < $seed.minSoilMoisture * $seed.getParam("urgent_moisture_factor", 0.65)
        )

        not DailyAnalysis(totalRain > $seed.heavyRainThreshold)
    then
        Recommendation rec = new Recommendation();
        rec.setFieldId($fid);
        rec.setRecommendedSeed(SeedType.WHITE_GRAPES);
        rec.setRecommendationType(RecommendationType.IRRIGATE_NOW);

        String urgency = "URGENT";
        String context = "";
        if ($stage == GrowthStage.MATURE) {
            urgency = "ATTENTION";
            context = "Ripening Phase: Severe stress now can affect berry size and yield.";
        } else {
             context = "Vines are severely stressed.";
        }

        double threshold = $seed.getAllowedWaterDeficit() * $seed.getParam("urgent_deficit_factor", 1.3);

        rec.setAdvice(urgency + ": Irrigate White Grapes (" + $stage + ")");
        rec.setReasoning(context + " Deficit: " + String.format("%.1f", $calc.getDeficit()) +
                         " mm (Threshold: " + String.format("%.1f", threshold) + " mm).");

        rec.getMetrics().put("deficit_amount", $calc.getDeficit());
        rec.getMetrics().put("crop_stage", $stage.toString());
        recommendations.add(rec);
end

rule "Irrigation Soon Warning - WHITE GRAPES"
    salience 40
    activation-group "whitegrapes-irrigation"
    when
        $status : FieldStatus(seedType == SeedType.WHITE_GRAPES, $stage : stage, $fid : fieldId)
        $seed   : Seed(seedType == SeedType.WHITE_GRAPES)

        $calc : WaterDeficitResult(
            seed == $seed,
            fieldId == $fid,
            deficit > $seed.allowedWaterDeficit * $seed.getParam("soon_deficit_min", 0.75),
            deficit <= $seed.allowedWaterDeficit * $seed.getParam("soon_deficit_max", 1.3)
        )

        $analysis : DailyAnalysis(
            currentSoilMoisture >= $seed.minSoilMoisture * $seed.getParam("soon_moisture_min", 0.55),
            currentSoilMoisture < $seed.minSoilMoisture * $seed.getParam("soon_moisture_max", 0.75)
        )

        not DailyAnalysis(totalRain > $seed.heavyRainThreshold)
    then
        Recommendation rec = new Recommendation();
        rec.setFieldId($fid);
        rec.setRecommendedSeed(SeedType.WHITE_GRAPES);
        rec.setRecommendationType(RecommendationType.IRRIGATE_SOON);

        double minThr = $seed.getAllowedWaterDeficit() * $seed.getParam("soon_deficit_min", 0.75);
        double maxThr = $seed.getAllowedWaterDeficit() * $seed.getParam("soon_deficit_max", 1.3);

        rec.setAdvice("Monitor White Grapes (" + $stage + ")");
        rec.setReasoning("Vines in quality window. Deficit: " + String.format("%.1f", $calc.getDeficit()) +
                         " mm. Warning Range: " + String.format("%.1f", minThr) + " - " + String.format("%.1f", maxThr) + " mm.");

        rec.getMetrics().put("deficit_amount", $calc.getDeficit());
        recommendations.add(rec);
end

rule "No Action Needed - WHITE GRAPES"
    salience 10
    activation-group "whitegrapes-irrigation"
    when
        $status : FieldStatus(seedType == SeedType.WHITE_GRAPES, $stage : stage, $fid : fieldId)
        $seed   : Seed(seedType == SeedType.WHITE_GRAPES)

        $calc : WaterDeficitResult(
            seed == $seed,
            fieldId == $fid,
            deficit <= $seed.allowedWaterDeficit * $seed.getParam("normal_deficit_limit", 0.75)
        )
        $analysis : DailyAnalysis(
            currentSoilMoisture >= $seed.minSoilMoisture
        )
    then
        Recommendation rec = new Recommendation();
        rec.setFieldId($fid);
        rec.setRecommendedSeed(SeedType.WHITE_GRAPES);
        rec.setRecommendationType(RecommendationType.CONTINUE_NORMAL);

        double limit = $seed.getAllowedWaterDeficit() * $seed.getParam("normal_deficit_limit", 0.75);

        rec.setAdvice("White Grapes Conditions Optimal (" + $stage + ")");
        rec.setReasoning("Moisture levels healthy. Deficit: " + String.format("%.1f", $calc.getDeficit()) +
                         " mm (Safe below: " + String.format("%.1f", limit) + " mm).");

        rec.getMetrics().put("deficit_amount", $calc.getDeficit());
        recommendations.add(rec);
end

rule "Monitor Conditions - WHITE GRAPES (Fallback)"
    salience 1
    activation-group "whitegrapes-irrigation"
    when
        $seed   : Seed(seedType == SeedType.WHITE_GRAPES)
        $status : FieldStatus(seedType == SeedType.WHITE_GRAPES, $stage : stage, $fid : fieldId)
        $calc   : WaterDeficitResult(seed == $seed, fieldId == $fid)
        $analysis : DailyAnalysis()
    then
        Recommendation rec = new Recommendation();
        rec.setFieldId($fid);
        rec.setRecommendedSeed(SeedType.WHITE_GRAPES);
        rec.setRecommendationType(RecommendationType.MONITOR_CONDITIONS);
        rec.setAdvice("Monitor White Grapes Conditions (" + $stage + ")");
        rec.setReasoning("Conditions stable. Monitor for powdery mildew if humidity is high. Deficit: " + String.format("%.1f", $calc.getDeficit()) + " mm.");
        rec.getMetrics().put("deficit_amount", $calc.getDeficit());
        rec.getMetrics().put("rule_fired", "whitegrapes-monitor-fallback");
        recommendations.add(rec);
end