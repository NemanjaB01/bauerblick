package rules.safety;

import com.agriscope.rule_engine.domain.model.CurrentWeatherData
import com.agriscope.rule_engine.domain.model.Seed
import com.agriscope.rule_engine.domain.model.Recommendation
import com.agriscope.rule_engine.domain.enums.SeedType
import com.agriscope.rule_engine.domain.enums.RecommendationType
import com.agriscope.rule_engine.domain.model.FieldStatus
import com.agriscope.rule_engine.domain.enums.GrowthStage

global java.util.List recommendations;

rule "Black Grapes - Frost Alert"
when
    $seed: Seed(seedType == SeedType.BLACK_GRAPES)
    $field: FieldStatus(seedType == SeedType.BLACK_GRAPES, stage != GrowthStage.READY)
    $weather: CurrentWeatherData(temperature_2m < $seed.minTemperature)
then
    Recommendation rec = new Recommendation();
    rec.setUserId($weather.getUserId());
    rec.setRecommendedSeed(SeedType.BLACK_GRAPES);
    rec.setRecommendationType(RecommendationType.FROST_ALERT);
    rec.setAdvice("IGNITE_FROST_CANDLES_OR_IRRIGATE");
    rec.setReasoning("Temperature (" + String.format("%.1f", $weather.getTemperature_2m()) + "C) below frost risk threshold (" + $seed.getMinTemperature() + "°C)");
    rec.setWeatherTimestamp($weather.getTime());
    recommendations.add(rec);
end

rule "Black Grapes - Heat Stress Alert"
when
    $seed: Seed(seedType == SeedType.BLACK_GRAPES)
    $field: FieldStatus(seedType == SeedType.BLACK_GRAPES, stage != GrowthStage.READY)
    $weather: CurrentWeatherData(temperature_2m > $seed.heatStressTemperature)
then
    Recommendation rec = new Recommendation();
    rec.setUserId($weather.getUserId());
    rec.setRecommendedSeed(SeedType.BLACK_GRAPES);
    rec.setRecommendationType(RecommendationType.HEAT_ALERT);
    rec.setAdvice("MANAGE_CANOPY_SHADE");
    rec.setReasoning("Temperature (" + String.format("%.1f", $weather.getTemperature_2m()) + "C) above heat stress threshold (" + $seed.getHeatStressTemperature() + "°C)");
    rec.setWeatherTimestamp($weather.getTime());
    recommendations.add(rec);
end

